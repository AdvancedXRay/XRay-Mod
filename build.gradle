buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net' }
        maven { url = 'https://repo.spongepowered.org/repository/maven-public/' }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
//        classpath group: 'org.spongepowered', name: 'mixingradle', version: '0.7.+'
    }
}

plugins {
    id "com.matthewprenger.cursegradle" version "1.4.0"
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'maven-publish'
//apply plugin: 'org.spongepowered.mixin'

def ENV = System.getenv()

version = "${game_version}-${mod_version}-build.${ENV.GITHUB_RUN_NUMBER ?: 9999}"
group= "pro.mikey.xray"
archivesBaseName = "advanced-xray-forge"

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

minecraft {
    mappings channel: 'official', version: "${game_version}"
//    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        client {
            workingDirectory project.file('run')

//            arg "-mixin.config=xray.mixins.json"

            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'

            mods {
                xray {
                    source sourceSets.main
                }
            }
        }
    }
}

//mixin {
//    add sourceSets.main, "xray.refmap.json"
//    config 'xray.mixins.json'
//}

dependencies {
    minecraft "net.minecraftforge:forge:${game_version}-${forge_version}"
//    annotationProcessor 'org.spongepowered:mixin:0.8.4:processor'
}

jar {
    manifest {
        attributes([
//                "MixinConfigs": 'xray.mixins.json',
                "Specification-Title": "Advanced XRay",
                "Specification-Vendor": "",
                "Specification-Version": "${version}",
                "Implementation-Title": project.name,
                "Implementation-Version": "${version}",
                "Implementation-Vendor" :"advanced-xray",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

task deobfJar(type: Jar) {
    from(sourceSets.main.output)
    archiveName = "${baseName}-${version}-deobf.${extension}"
}

task sourcesJar(type: Jar) {
    from(sourceSets.main.allSource)
    archiveName = "${baseName}-${version}-sources.${extension}"
}

artifacts {
    archives deobfJar
    archives sourcesJar
}

jar.finalizedBy('reobfJar')

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId project.archivesBaseName
            version project.version
            artifact jar

            artifact deobfJar {
                classifier 'deobf'
            }

            artifact sourcesJar {
                classifier 'sources'
            }
        }
    }
    repositories {
        maven {
            url "https://maven.saps.dev/releases"
            credentials {
                username = "mikeymods"
                password = "${ENV.SAPS_TOKEN}"
            }
        }
    }
}

if (ENV.CURSE_DEPLOY_TOKEN) {
    curseforge {
        apiKey = ENV.CURSE_DEPLOY_TOKEN
        project {
            id = project.curse_id
            releaseType = "release"
            addGameVersion "Forge"
            addGameVersion "$game_version"
            changelog = "See [Commit history](https://github.com/MichaelHillcox/XRay-Mod/commits/main)"
            changelogType = 'markdown'
            mainArtifact(jar)
        }
    }
}
